%{
	#include <stdio.h>
	#include <stdlib.h>
	#include <iostream>

	//#include "lex.yy.h"
	
	#include "../symbols/symbol.h"
	#include "gsc.tab.hpp"

	int yylex(YYSTYPE *yylval_param, YYLTYPE *yylloc_param, void* yyscanner);
	//extern int yyparse();
	void yyerror(YYLTYPE* loc, void* scanner, const char* err);
%}

%define api.pure full
%lex-param { void* scanner }
%parse-param { void* scanner }

%define parse.error verbose

%locations
%initial-action
{
	#if VSCODE_COMPATIBLE_LOCATION
		@$.first_line = @$.last_line = 0;
		@$.first_column = @$.last_column = 0;
	#endif
}

%code requires
{
	#include "../symbols/operator_enum.h"
	
	//
	// Forward declarations for any custom classes used in YYSTYPE
	//
	class Symbol;
	class Include;
	
	class Literal;
	class Identifier;
	
	class Animtree;
	class Expression;
	class Reference;
	class Pointer;
	
	class Function;
	class Call;
	
	class Return;
}

%union {
	OPERATOR_TYPE	op; //operator
	
	int			literal_int;
	float		literal_float;
	char*		literal_string;
	
	char*		t_identifier;
	
	Symbol*		symbol;
	Include*	include;
	Animtree*	animtree;
	
	Literal*	literal;
	
	Expression* expression;
	Reference* reference;
	Pointer* pointer;
	
	Function* function;
	Call* call;
	
	Return* retn;
}

%token<literal_string> T_STRING
%token<literal_string> T_FLOAT
%token<literal_string> T_INT

%token INCLUDE
%token USING_ANIMTREE
%token ANIMTREE

%token OP_LPAREN
%token OP_RPAREN
%token FUNC_POINTER_BEGIN

%token OP_LBRACKET
%token OP_RBRACKET
%token OP_LBRACE
%token OP_RBRACE
%token OP_COMMA
%token OP_INC
%token OP_DEC

%token OP_COLON_DOUBLE
%token OP_COLON
%token OP_COLON_SEMI
%token OP_QMARK
%token OP_HASH

%token T_IF
%token T_ELSE
%token T_SWITCH
%token T_CASE
%token T_DEFAULT
%token T_BREAK
%token T_FOR
%token T_WHILE
%token T_CONTINUE
%token T_RETURN
%token T_THREAD
%token T_WAIT
%token<literal_string> T_FILEPATH
%token<t_identifier> T_IDENTIFIER

// Define Token
%token OP_INC_POST OP_DEC_POST OP_LB OP_RB OP_DOT
%token OP_INC_PRE OP_DEC_PRE OP_UPLUS OP_UMINUS OP_NOT OP_BW_NOT
%token OP_MULT OP_DIV OP_MOD
%token OP_ADD OP_SUB
%token OP_LSHIFT OP_RSHIFT
%token OP_CMP_LT OP_CMP_LE OP_CMP_GT OP_CMP_GE
%token OP_CMP_EQ OP_CMP_NEQ
%token OP_BW_AND
%token OP_BW_XOR
%token OP_BW_OR
%token OP_CMP_AND
%token OP_CMP_OR
//ternary
%token OP_ASSIGN OP_ASSIGN_ADD OP_ASSIGN_SUB OP_ASSIGN_MULT OP_ASSIGN_DIV OP_ASSIGN_BW_AND OP_ASSIGN_BW_XOR OP_ASSIGN_BW_OR

%token T_INVALID
%token T_EOF 0

// Operator Precedence ( highest line number = highest precedence )
%right OP_ASSIGN OP_ASSIGN_ADD OP_ASSIGN_SUB OP_ASSIGN_MULT OP_ASSIGN_DIV OP_ASSIGN_BW_AND OP_ASSIGN_BW_XOR OP_ASSIGN_BW_OR
//ternary
%left OP_CMP_OR
%left OP_CMP_AND
%left OP_BW_OR
%left OP_BW_XOR
%left OP_BW_AND
%left OP_CMP_EQ OP_CMP_NEQ
%left OP_CMP_LT OP_CMP_LE OP_CMP_GT OP_CMP_GE
%left OP_LSHIFT OP_RSHIFT
%left OP_ADD OP_SUB
%left OP_MULT OP_DIV OP_MOD
%right OP_INC_PRE OP_DEC_PRE OP_UPLUS OP_UMINUS OP_NOT OP_BW_NOT
%left OP_INC_POST OP_DEC_POST OP_LB OP_RB OP_DOT

//%type<ival> expression
//%type<fval> mixed_expression

%type<symbol> 		IncludeDirective;
%type<symbol> 		AnimtreeDirective;
%type<symbol> 		FunctionDeclaration;
		
%type<symbol> 		FormalParameterList;
		
%type<symbol> 		SourceElements;
%type<symbol> 		SourceElement;
		
%type<literal> 		StringLiteral;
%type<literal> 		NumericLiteral;
%type<symbol> 		Block;
%type<symbol> 		FunctionParameterList;
%type<call>			FunctionCall;
%type<symbol>		FunctionExpression;
%type<pointer>		PointerExpression;
%type<reference>	ReferenceExpression;
%type<symbol> 		AnimReferenceExpression;
%type<symbol> 		MemberExpression;
%type<symbol> 		ElementList;
%type<symbol> 		ListExpression;
%type<symbol> 		ObjectExpression;	
%type<symbol> 		LiteralExpression;
%type<symbol> 		OptionalExpression;
%type<expression>	BasicExpression;
%type<op> 			OperatorPostfix;
%type<op> 			OperatorPrefix;	
%type<op> 			OperatorMid;
%type<expression>	e;
%type<expression> 	Expression;
%type<symbol> 		ExpressionStatement;
%type<symbol> 		ReturnStatement;
%type<symbol> 		WaitStatement;
%type<symbol> 		EmptyStatement;
%type<symbol> 		IfStatement;
%type<symbol> 		SwitchStatement;
%type<symbol> 		CaseStatement;
%type<symbol> 		LoopStatement;
%type<symbol> 		Statement;
%type<symbol> 		StatementList;

%destructor { printf("BISON SYMBOL DESTRUCTOR\n"); } <*>

%start Program

%%

IncludeDirective:
	INCLUDE T_FILEPATH OP_COLON_SEMI
		{
			$$ = new Include(new Literal($2, @2, S_TYPE_LITERAL_FILEPATH), @$); 
		};
		
AnimtreeDirective:
	USING_ANIMTREE OP_LPAREN T_STRING OP_RPAREN OP_COLON_SEMI
		{
			$$ = new Animtree(new Literal($3, @3, S_TYPE_LITERAL_STRING), @$); 
		};

FunctionDeclaration:
	T_IDENTIFIER OP_LPAREN FormalParameterList OP_RPAREN Block
	{
		$$ = new Function(new Identifier($1, @1), @$);
		$$->AddChild(new Identifier($1,@1));
		$$->AddChild(new Group($3, @3));
		$$->AddChild($5);
	};

FormalParameterList
	: FormalParameterList OP_COMMA T_IDENTIFIER
		{
			$1->AddToEnd( new Identifier($3, @3) );
			$$ = $1;
		}
	| T_IDENTIFIER
		{ $$ = new Identifier($1, @1); }
	|
		{ $$ = NULL; };

SourceElement:
	IncludeDirective
	| AnimtreeDirective
	| FunctionDeclaration

SourceElements:
	SourceElements SourceElement
		{
			$1->AddToEnd($2);
			$$ = $1;
		}
	| SourceElement
	;

Program: SourceElements T_EOF
	{
		for(Symbol* a = $1; a; a = a->NextElem())
		{
			a->PrintInfoRecursive();
		}
		
		printf("\nFILE RANGE: %d[%d] - %d[%d]\n", @$.first_line, @$.first_column, @$.last_line, @$.last_column);
	}
	
StringLiteral
	: T_STRING 
		{ $$ = new Literal($1, @1, S_TYPE_LITERAL_STRING); }
	| OP_BW_AND T_STRING 
		{ $$ = new Literal($2, @$, S_TYPE_LITERAL_STRING); }
	| OP_HASH T_STRING 
		{ $$ = new Literal($2, @$, S_TYPE_LITERAL_STRING); }
	;

NumericLiteral
	: T_INT 
		{ $$ = new Literal($1, @1, S_TYPE_LITERAL_INT); }
	| T_FLOAT 
		{ $$ = new Literal($1, @1, S_TYPE_LITERAL_FLOAT); }
	;

Block
	: OP_LBRACE StatementList OP_RBRACE 
		{ $$ = new Group($2, @2); }
	;

FunctionParameterList
	: Expression 
		{ $$ = $1; }
	| FunctionParameterList OP_COMMA Expression 
		{
			$1->AddToEnd($3);
			$$ = $1;
		}
	|  
		{ $$ = new Symbol(@$); }
	;

FunctionCall
	: T_IDENTIFIER OP_LPAREN FunctionParameterList OP_RPAREN 
		{
			$$ = new Call(@$, CALL_FLAG_IDENTIFIER);
			$$->AddChild(new Identifier($1,@1));
			$$->AddChild($3);
		}
	| T_THREAD T_IDENTIFIER OP_LPAREN FunctionParameterList OP_RPAREN 
		{
			$$ = new Call(@$, CALL_FLAG_THREAD | CALL_FLAG_IDENTIFIER);
			$$->AddChild(new Identifier($2,@2));
			$$->AddChild($4);
		}
	| PointerExpression OP_LPAREN FunctionParameterList OP_RPAREN 
		{
			$$ = new Call(@$, CALL_FLAG_POINTER);
			$$->AddChild($1);
			$$->AddChild($3);
		}
	| T_THREAD PointerExpression OP_LPAREN FunctionParameterList OP_RPAREN 
		{
			$$ = new Call(@$, CALL_FLAG_THREAD | CALL_FLAG_POINTER);
			$$->AddChild($2);
			$$->AddChild($4);
		}
	| ReferenceExpression OP_LPAREN FunctionParameterList OP_RPAREN 
		{
			$$ = new Call(@$, CALL_FLAG_REFERENCE);
			$$->AddChild($1);
			$$->AddChild($3);
		}
	| T_THREAD ReferenceExpression OP_LPAREN FunctionParameterList OP_RPAREN 
		{
			$$ = new Call(@$, CALL_FLAG_THREAD | CALL_FLAG_REFERENCE);
			$$->AddChild($2);
			$$->AddChild($4);
		}
	;

FunctionExpression
	: ObjectExpression FunctionCall 
		{
			// Add Object Expression Child
			$$ = $2;
		}
	| FunctionCall 
		{ $$ = $1; }
	;

PointerExpression
	: FUNC_POINTER_BEGIN ObjectExpression OP_RBRACKET OP_RBRACKET 
		{ $$ = new Pointer(@$); }
	| FUNC_POINTER_BEGIN ReferenceExpression OP_RBRACKET OP_RBRACKET 
		{ $$ = new Pointer(@$); }
	;

ReferenceExpression
	: T_FILEPATH OP_COLON_DOUBLE T_IDENTIFIER 
		{
			Literal* file = new Literal($1, @1, S_TYPE_LITERAL_FILEPATH);
			Identifier* identifier = new Identifier($3, @3);
			$$ = new Reference(file, identifier, @$);
		}
	| OP_COLON_DOUBLE T_IDENTIFIER 
		{
			Identifier* identifier = new Identifier($2, @2);
			$$ = new Reference(NULL, identifier, @$);
		}
	;

AnimReferenceExpression
	: OP_MOD T_IDENTIFIER 
		{ $$ = new Identifier($2, @2); }
	;

MemberExpression
	: ObjectExpression OP_LBRACKET Expression OP_RBRACKET 
		{ $$ = new Symbol(@$); }
	| ObjectExpression OP_DOT ObjectExpression 
		{ $$ = new Symbol(@$); }
	| OP_LBRACKET OP_RBRACKET 
		{ $$ = new Symbol(@$); }
	;

ElementList
	: Expression OP_COMMA Expression //Lists must have at least two elements 
		{ $$ = new Symbol(@$); }
	| ElementList OP_COMMA Expression 
		{ $$ = new Symbol(@$); }
	;

ListExpression
	: OP_LPAREN ElementList OP_RPAREN 
		{ $$ = new Symbol(@$); }
	;

ObjectExpression
	: T_IDENTIFIER 
		{ $$ = new Identifier($1, @1); }
	| FunctionExpression
	| MemberExpression 
	| OP_LPAREN ObjectExpression OP_RPAREN 
		{ $$ = $2; }
	;
	
LiteralExpression
	: NumericLiteral 
	| StringLiteral 
	;

OptionalExpression
	: Expression 
		{ $$ = $1; }
	| 
		{ $$ = NULL; }
	;

BasicExpression
	: ObjectExpression 
		{ $$ = (Expression*)$1; }
	| LiteralExpression 
		{ $$ = (Expression*)$1; }
	| ListExpression //used for things like vectors 
		{ $$ = new Expression(@$); }
	| ReferenceExpression 
		{ $$ = (Expression*)$1; }
	| ANIMTREE 
		{ $$ = new Expression(@$); }
	;

OperatorPostfix
	: OP_INC
		{ $$ = OP_TYPE_INC; };
	| OP_DEC
		{ $$ = OP_TYPE_DEC; };
	;
	
OperatorPrefix
	: OP_INC
		{ $$ = OP_TYPE_INC; };
	| OP_DEC
		{ $$ = OP_TYPE_DEC; };
	| OP_ADD
		{ $$ = OP_TYPE_ADD; };
	| OP_SUB
		{ $$ = OP_TYPE_SUB; };
	| OP_BW_NOT
		{ $$ = OP_TYPE_BW_NOT; };
	| OP_NOT
		{ $$ = OP_TYPE_NOT; };
	;
	
OperatorMid
	: OP_MULT 
		{ $$ = OP_TYPE_MULT; };
	| OP_DIV
		{ $$ = OP_TYPE_DIV; };
	| OP_MOD
		{ $$ = OP_TYPE_MOD; };
	| OP_ADD
		{ $$ = OP_TYPE_ADD; };
	| OP_SUB
		{ $$ = OP_TYPE_SUB; };
	| OP_LSHIFT
		{ $$ = OP_TYPE_LSHIFT; };
	| OP_RSHIFT
		{ $$ = OP_TYPE_RSHIFT; };
	| OP_CMP_LT
		{ $$ = OP_TYPE_CMP_LT; };
	| OP_CMP_LE
		{ $$ = OP_TYPE_CMP_LE; };
	| OP_CMP_GT
		{ $$ = OP_TYPE_CMP_GT; };
	| OP_CMP_GE
		{ $$ = OP_TYPE_CMP_GE; };
	| OP_CMP_EQ
		{ $$ = OP_TYPE_CMP_EQ; };
	| OP_CMP_NEQ
		{ $$ = OP_TYPE_CMP_NEQ; };
	| OP_BW_AND
		{ $$ = OP_TYPE_BW_AND; };
	| OP_BW_OR
		{ $$ = OP_TYPE_BW_OR; };
	| OP_CMP_AND
		{ $$ = OP_TYPE_CMP_AND; };
	| OP_CMP_OR
		{ $$ = OP_TYPE_CMP_OR; };
	| OP_ASSIGN
		{ $$ = OP_TYPE_ASSIGN; };
	| OP_ASSIGN_ADD
		{ $$ = OP_TYPE_ASSIGN_ADD; };
	| OP_ASSIGN_SUB
		{ $$ = OP_TYPE_ASSIGN_SUB; };
	| OP_ASSIGN_MULT
		{ $$ = OP_TYPE_ASSIGN_MULT; };
	| OP_ASSIGN_DIV
		{ $$ = OP_TYPE_ASSIGN_DIV; };
	| OP_ASSIGN_BW_AND
		{ $$ = OP_TYPE_ASSIGN_BW_AND; };
	| OP_ASSIGN_BW_XOR
		{ $$ = OP_TYPE_ASSIGN_BW_XOR; };
	| OP_ASSIGN_BW_OR
		{ $$ = OP_TYPE_ASSIGN_BW_OR; };
	;

e
	: BasicExpression 
		{ $$ = $1; }
	| e OperatorPostfix 
		{ $$ = new Expression($1, $2, @$); }
	| OperatorPrefix e 
		{ $$ = new Expression($1, $2, @$); }
	| e OperatorMid e 
		{ $$ = new Expression($1, $2, $3, @$); }
	| AnimReferenceExpression 
		{ $$ = (Expression*)$1; }
	| OP_LPAREN e OP_RPAREN 
		{ $$ = $2; }
	;

Expression
	: e 
		{ $$ = $1; }
	;
	
ExpressionStatement
	: Expression OP_COLON_SEMI 
		{ $$ = $1; /*new Symbol(@$);*/ }
	;

ReturnStatement
	: T_RETURN OP_COLON_SEMI 
		{ $$ = new Return(NULL, @$); }
	| T_RETURN Expression OP_COLON_SEMI 
		{ $$ = new Return($2, @$); }
	;

WaitStatement
	: T_WAIT Expression OP_COLON_SEMI 
		{ $$ = new Symbol(@$); }
	| T_WAIT OP_LPAREN Expression OP_RPAREN OP_COLON_SEMI 
		{ $$ = new Symbol(@$); }
	;

EmptyStatement
	: OP_COLON_SEMI
		{ $$ = new Symbol(@$); }
	;

IfStatement
	: T_IF OP_LPAREN Expression OP_RPAREN Statement 
		{ $$ = new Symbol(@$); }
	| T_ELSE Statement
		{ $$ = new Symbol(@$); }
	;

SwitchStatement
	: T_SWITCH OP_LPAREN Expression OP_RPAREN Statement 
		{ $$ = new Symbol(@$); }
	;

CaseStatement
	: T_CASE LiteralExpression OP_COLON 
		{ $$ = new Symbol(@$); }
	| T_DEFAULT OP_COLON 
		{ $$ = new Symbol(@$); }
	;

LoopStatement
	: T_WHILE OP_LPAREN Expression OP_RPAREN Statement 
		{ $$ = new Symbol(@$); }
	| T_FOR OP_LPAREN OptionalExpression OP_COLON_SEMI OptionalExpression OP_COLON_SEMI OptionalExpression OP_RPAREN Statement 
		{
			$$ = new Symbol(@$);
		}
	;
		
Statement
	: Block 
	| WaitStatement 
	| ExpressionStatement 
	| IfStatement 
	| SwitchStatement 
	| CaseStatement 
	| LoopStatement 
	| ReturnStatement
	| T_BREAK OP_COLON_SEMI 
		{ $$ = new Symbol(@$); }
	| T_CONTINUE OP_COLON_SEMI 
		{ $$ = new Symbol(@$); }
	| EmptyStatement 
	;

StatementList
	: StatementList Statement 
		{
			if($1 != NULL)
			{
				$1->AddToEnd($2);
				$$ = $1;
			}
			else
			{
				$$ = $2;
			}
		}
	| 
		{ $$ = NULL; }
	;

%%

#include <sys/time.h>

#define DEBUG 1

void yyerror(YYLTYPE* loc, void* scanner, const char* err) {
	fprintf(stderr, "PARSE ERROR AT LINE %d(%d): %s\n",
		loc->first_line, loc->first_column,
		err);
		
#if !(DEBUG)
	exit(1);
#endif
}

extern int yylex_init(void** ptr_yy_globals);
extern int yylex_destroy(void* yyscanner);
extern void yyset_in(FILE* in, void* scanner);
extern void yyset_out(FILE* out, void* scanner);

int main(int argc, char** argv) {
	FILE* in = argc > 1 ? fopen(argv[1], "r") : stdin;
	
#if DEBUG
while(1){
#endif

	timespec start;
	clock_gettime(CLOCK_REALTIME, &start);

	void* myscanner;
	yylex_init(&myscanner);
	
	yyset_in(in, myscanner );
	yyset_out(stdout, myscanner);
	
	yyparse(myscanner);
	yylex_destroy(myscanner);
	
	timespec end;
	clock_gettime(CLOCK_REALTIME, &end);
	
	if(argc > 1)
	{
		int nanos = end.tv_nsec - start.tv_nsec;
		fprintf(stderr, "Parsed in %f ms\n", (double)nanos / 1000000.0);
		
		printf("\n");
		exit(0);
	}
	
#if DEBUG
}
#endif

	return 0;
}
